plugins {
    id "dev.architectury.loom" version "1.3-SNAPSHOT"
    id "maven-publish"
}

base {
    archivesName = project.archives_base_name
}

version = project.mod_version
group = project.maven_group

java {
    sourceCompatibility = targetCompatibility = JavaVersion.VERSION_17
}

loom {
    silentMojangMappingsLicense()
    forge {
        mixinConfigs = [
                "kjspncr.mixins.json"
        ]
    }
}

repositories {
    maven {
        name = "Dan's Maven (Architectury)"
        url = "https://maven.architectury.dev"
        content {
            includeGroup "dev.architectury"
        }
    }

    maven {
        name = "Lat's Maven (Rhino, KubeJS)"
        url = "https://maven.saps.dev/minecraft"
        content {
            includeGroup "dev.latvian.mods"
        }
    }

    exclusiveContent {
        forRepository {
            maven {
                name = "Modrinth's Maven (PNCR & extras)"
                url = "https://api.modrinth.com/maven"
            }
        }
        filter {
            includeGroup "maven.modrinth"
        }
    }
}

dependencies {
    /* MC Dependencies */
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings loom.officialMojangMappings()
    forge "net.minecraftforge:forge:${project.forge_version}"

    /* Kube Dependencies */
    modImplementation("dev.latvian.mods:kubejs-forge:${kjs_ver}")
    modRuntimeOnly("dev.latvian.mods:rhino-forge:${rhino_ver}")
    modRuntimeOnly("dev.architectury:architectury-forge:${arch_ver}")

    /* These extras are !required! for KubeJS to work! */
    compileOnly(annotationProcessor("io.github.llamalad7:mixinextras-common:0.3.2"))
    implementation(include("io.github.llamalad7:mixinextras-forge:0.3.2"))

    /* PNCR */
    modImplementation("maven.modrinth:pneumaticcraft-repressurized:${pncr_ver}")

    /* Extras */
    modRuntimeOnly("maven.modrinth:embeddium:0.3.7+mc1.20.1")
    modRuntimeOnly("maven.modrinth:probejs:6.0.0")
    modRuntimeOnly("maven.modrinth:rei:12.0.684+forge")
    modRuntimeOnly("maven.modrinth:roughly-enough-items-hacks:12.0.93")
    modRuntimeOnly("maven.modrinth:cloth-config:11.1.118+forge")
}

processResources {
    inputs.property "version", project.version
    filesMatching("META-INF/mods.toml") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
    options.release = 17
}

java {
    withSourcesJar()
}

jar {
    manifest {
        attributes([
                "Specification-Title"     : project.mod_id,
                "Specification-Vendor"    : project.mod_author,
                "Specification-Version"   : "1",
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : version,
                "Implementation-Vendor"   : project.mod_author,
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }

    repositories {
    }
}
